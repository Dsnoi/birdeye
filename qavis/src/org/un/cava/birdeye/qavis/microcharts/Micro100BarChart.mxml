<?xml version="1.0" encoding="utf-8"?>
<Surface xmlns="http://www.degrafa.com/2008"   
	xmlns:mx="http://www.adobe.com/2006/mxml">
	
	<mx:Script>
		<![CDATA[
/*  
 * The MIT License
 *
 * Copyright (c) 2008
 * United Nations Office at Geneva
 * Center for Advanced Visual Analytics
 * http://cava.unog.ch
 * 
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 * 
 * The above copyright notice and this permission notice shall be included in
 * all copies or substantial portions of the Software.
 * 
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN
 * THE SOFTWARE.
 */
 
			import mx.events.IndexChangedEvent;
/**
 * <p>This component is used to create 100% Bar microcharts. 
 * The basic simple syntax to use it and create an 100% Bar microchart with mxml is:</p>
 * <p>&lt;Micro100BarChart dataProvider="{myArray}" width="20" height="70"/></p>
 * 
 * <p>The dataProvider property can only accept Array at the moment, but will be soon extended with ArrayCollection
 * and XML. To insure the proper sizing binding, it's better to use the following properties:</p>
 * <p>- sizeX: to define the width of the chart;</p>
 * <p>- sizeY: to define the height of the chart;  </p>
 * <p>It's also possible to change the colors by defining the following Array of int:</p>
 * <p>- colors: array that sets the color for each bar value. The lenght has to be the same as the dataProvider.</p>
 * 
 * <p>If no colors are defined, than the 100 bar will display different colors based on the default color and a default offset color.</p>
*/
			private var tot:Number = NaN;
			
			[Bindable]
			private var tempColor:int = 0xbbbbbb;
			
			[Bindable]
			private var _sizeY:Number = NaN;
			
			[Bindable]
			private var _sizeX:Number = NaN;

			[Bindable]
			private var _dataProvider:Array = new Array();
			
			[Bindable]
			private var _colors:Array = null;

			[Bindable]
			public function set sizeX(val:Number):void
			{
				_sizeX = val;
			}
			
			/**
			* Set the width parameter of the chart. 
			* At this stage, it's recommended to use sizeX together with width to avoid rendering and binding problems. 
			*/
			public function get sizeX():Number
			{
				return _sizeX;
			}
						
			[Bindable]
			public function set sizeY(val:Number):void
			{
				_sizeY = val;
			}
			
			/**
			* Set the height parameter of the chart. 
			* At this stage, it's recommended to use sizeY together with height to avoid rendering and binding problems. 
			*/
			public function get sizeY():Number
			{
				return _sizeY;
			}
						
			[Bindable]
			public function set dataProvider(val:Array):void
			{
				setTot(val);
				_dataProvider = val;
			}
			
			/**
			* Set the dataProvider to feed the chart. 
			*/
			public function get dataProvider():Array
			{
				return _dataProvider;
			}
					
			[Bindable]
			public function set colors(val:Array):void
			{
				_colors = val;
			}
			
			/**
			 * <p>The Micro100BarChart component is used to create 100% Bar microcharts. 
			 * The basic simple syntax to use it and create an 100% Bar microchart with mxml is:</p>
			 * <p>&lt;Micro100BarChart dataProvider="{myArray}" width="20" height="70"/></p>
			 * 
			 * <p>The dataProvider property can only accept Array at the moment, but will be soon extended with ArrayCollection
			 * and XML. To insure the proper sizing binding, it's better to use the following properties:</p>
			 * <p>- sizeX: to define the width of the chart;</p>
			 * <p>- sizeY: to define the height of the chart;  </p>
			 * <p>It's also possible to change the colors by defining the following Array of int:</p>
			 * <p>- colors: array that sets the color for each bar value. The lenght has to be the same as the dataProvider.</p>
			 * 
			 * <p>If no colors are defined, than the 100 bar will display different colors based on the default color and a default offset color.</p>
			 * 
			 * This property sets the colors of the bars in the chart. If not set, a function will automatically create colors for each bar.
			*/
			public function get colors():Array
			{
				return _colors;
			}
					
			/**
			* @private  
			* Calculate the total of all positive values in the dataProvider. Negative values are not considered nor rendered in the chart. 
			*/
			private function setTot(val:Array):void
			{
				tot = 0;
				for (var i:Number = 0; i < val.length; i++)
					if (val[i] > 0)
						tot += val[i];
			}
			
			/**
			* @private  
			* Calculate the width size of the current value provided by the repeater. 
			*/
			private function offsetSizeX(indexIteration:Number, target:Object):Number
			{
				if (isNaN(tot)) 
					setTot(_dataProvider);
				var _offSizeX:Number = Math.max(0,_dataProvider[indexIteration] * width / tot); 
				return _offSizeX;
			}
			
			/**
			* @private  
			* Calculate the offset x position from where the next bar will be drawn. 
			*/
			private function startX(indexIteration:Number, target:Object):Number
			{
				if (isNaN(tot))
					setTot(_dataProvider);
				var _startX:Number = (indexIteration==0)? 0 : Math.max(0,_dataProvider[indexIteration-1] * width / tot);
				return _startX;
			}
			
			/**
			* @private  
			* Set automatic colors to the bars, in case these are not provided. 
			*/
			private function useColor(indexIteration:Number, target:Object):int
			{
				if (colors != null && colors.length > 0)
					tempColor = colors[indexIteration];
				else
					tempColor += 0x123456; 

				return tempColor;
			}
		]]>
	</mx:Script>
		
	<graphicsData>
		<GeometryGroup>
			<geometry>
				<GeometryRepeater count="{_dataProvider.length}">
					<RegularRectangle height="{sizeY}" width="{sizeX}"
						stroke="{black}">
						<fill>
							<SolidFill color="{tempColor}" alpha="1"/>
						</fill>
					</RegularRectangle>
					<modifiers>
						<PropertyModifier property="x" offset="{startX}" offsetOperator="add"/>
						<PropertyModifier property="width" offset="{offsetSizeX}" offsetOperator="none"/>
						<PropertyModifier property="fill.color" offset="{useColor}" offsetOperator="none"/>
					</modifiers>
				</GeometryRepeater>
			</geometry>
			<strokes>
				<SolidStroke id="black" color="0x000000" alpha="1"/>
			</strokes>
		</GeometryGroup>
	</graphicsData>
</Surface>