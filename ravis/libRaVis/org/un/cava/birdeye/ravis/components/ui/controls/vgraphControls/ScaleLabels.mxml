<?xml version="1.0" encoding="utf-8"?>
<!--
 *
 * The MIT License
 *
 * Copyright (c) 2008
 * United Nations Office at Geneva
 * Center for Advanced Visual Analytics
 * http://cava.unog.ch
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 * 
 * The above copyright notice and this permission notice shall be included in
 * all copies or substantial portions of the Software.
 * 
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN
 * THE SOFTWARE.
 *
-->
<mx:VBox 
	xmlns:mx="http://www.adobe.com/2006/mxml"
	creationComplete="registerComplete()"
	>
	
	<mx:Label
		toolTip="Use this control to increase/decrease text/icon size."
		text="Scaling"
		fontSize="9" 
		textAlign="center"
		/>
		
	<mx:HBox width="95%">
		<mx:Button
			id="minusScale"
			icon="{EmbeddedIcons.zoomoutIcon}"
			cornerRadius="5"
			click="scaleSlider.value=scaleSlider.value-.1; updateScale();"
			/>		
		<mx:HSlider
			tickInterval=".25"
			width="95%"
			id="scaleSlider"
			value="1"
			minimum=".5"
			maximum="2.0"
			snapInterval=".1"
			liveDragging="true"
			change="updateScale()"
			/>
		<mx:Button
			id="plusScale"
			icon="{EmbeddedIcons.zoominIcon}"
			cornerRadius="5"
			click="scaleSlider.value=scaleSlider.value+.1; updateScale();"
			/>	
	</mx:HBox>

	<mx:Script>
		<![CDATA[
			import org.un.cava.birdeye.ravis.graphLayout.layout.ILayoutAlgorithm;
			import mx.core.Container;
			import mx.core.UIComponent;
			import org.un.cava.birdeye.ravis.globals.GlobalParams;
			import org.un.cava.birdeye.ravis.globals.GlobalParamsVGraph;
			import org.un.cava.birdeye.ravis.graphLayout.visual.IVisualGraph;
			import org.un.cava.birdeye.ravis.assets.icons.EmbeddedIcons;
			
			/* to be set if creation was complete */
			private var _complete:Boolean = false;
			
			/**
			 * this updates the scale factor in all children of the vgraph
			 * */	
			public function updateScale():void {
				
				/* we need the VGraph object in its capacity as a Container */
				var vgraph:Container = (GlobalParams.vgraph as Container);
				var children:Array;
				var child:UIComponent;

				var layouter:ILayoutAlgorithm = GlobalParams.layouter;

				if(vgraph != null) {		
					/* update the global parameter */
					GlobalParams.scaleFactor = scaleSlider.value;
					
					children = vgraph.getChildren();
					for each(child in children) {
						child.scaleX = scaleSlider.value;
						child.scaleY = scaleSlider.value;
					}
					
					/* check if we have a layouter at all */
					if(layouter != null) {
						layouter.layoutChanged = true;
					} else {
						trace("Cannot notify null layouter of layout change due to scaling.");
					}
	
					/* a bit ugly, but we need the polymorphism here, since the
					 * IVisualGraph interface does not contain all the Container
					 * methods. Our object of course does! */
					(vgraph as IVisualGraph).draw(); // do we need this? 
					
				} else {
					trace("Scale NOT changed, no valid vgraph in GlobalParams, not redrawing!");
				}
			}
			
			/**
			 * refreshes the slider from the current scale value
			 * of the first child object of VGraph
			 * */
			public function refreshSlider():void {
				
				/* we need the VGraph object in its capacity as a Container */
				var vgraph:Container = (GlobalParams.vgraph as Container);
				var children:Array;
				var child:UIComponent;

				if(vgraph != null) {		
					
					/* get from first child. XXX Needs to check that
					 * we actually have at least 1 child (we should
					 * at least always have the drawing surface for the edges.....
					 */
					scaleSlider.value = vgraph.getChildAt(0).scaleX;
					
					/* npw update the global parameter */
					GlobalParams.scaleFactor = scaleSlider.value;
				} else {
					trace("Scale NOT refreshed, no valid vgraph in GlobalParams!");
				}
			}
			
			/**
			 * register that we are complete
			 * */
			private function registerComplete():void {
				_complete = true;
				
				/* register ourselves */
				GlobalParamsVGraph.scaleLabelsControl = this;
			}
		]]>
	</mx:Script>
</mx:VBox>
